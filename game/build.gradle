plugins {
    id "io.freefair.lombok" version "5.3.0" apply false
    id "com.diffplug.spotless" version "5.9.0" apply false
}

allprojects {
    ext {
        appName = "game"
        slf4jVersion = '1.7.30'
        gdxVersion = '1.9.13'
    }
    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: "java"
    apply plugin: "io.freefair.lombok"
    apply plugin: "com.diffplug.spotless"

    version = '1.0'
    group = 'com.demo.gradle'
    sourceCompatibility = 11

    dependencies {
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation "org.slf4j:slf4j-simple:$slf4jVersion"

        // https://github.com/junit-team/junit5-samples/blob/r5.7.1/junit5-jupiter-starter-gradle/build.gradle
        testImplementation platform('org.junit:junit-bom:5.7.1')
	      testImplementation('org.junit.jupiter:junit-jupiter')
    }

    spotless {
        java {
            importOrder('java', 'java', 'com.badlogic.gdx', 'com.chasbob', '')
            removeUnusedImports()
            googleJavaFormat('1.9')
        }
        groovyGradle {
            target '*.gradle'
            greclipse()
        }
    }

    build.dependsOn spotlessApply

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

project(":core") {
    apply plugin: "java-library"

    dependencies {
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    }
}

project(":desktop") {
    apply plugin: "application"

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        
    }

    sourceSets {
        main {
            resources {
                // srcDir project(':core').sourceSets.main.resources
                srcDirs += [
                    project(':core').sourceSets.main.resources
                ]
            }
        }
    }

    application {
        getMainClass().set("com.chasbob.game.desktop.DesktopLauncher")
    }
}
